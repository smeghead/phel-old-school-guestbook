(ns old-school-guestbook\view\main
  (:require phel\html :refer [html doctype]))

(def- head
  [:head
   [:meta {:charset "utf-8"}]
   [:meta {:name "viewport" :content "width=device-width, initial-scale=1"}]
   [:link {:href "https://fonts.xz.style/serve/inter.css" :rel "stylesheet"}]
   [:link {:href "https://cdn.jsdelivr.net/npm/@exampledev/new.css@1.1.2/new.min.css" :rel "stylesheet"}]
   [:link {:rel "stylesheet" :href "./css/style.css"}]
   [:link {:rel "icon" :type "image/png" :href "./images/favicon-32.png"}]
   [:title "Old School Guestbook"]])

(defn- content [messages params errors]
  [:body
    [:h1 "Old School Guestbook"]
    `[:ul ,@(if-not (empty? errors)
           (map (fn [x] [:li (x :message)]) errors))]
    [:form {:method "POST"}
      [:p
        [:label "Name"]
        [:br]
        [:input {:type "text" :name "name" :value (params "name")}]]
      [:p
        [:label "Message"]
        [:br]
        [:textarea {:name "message"} (params "message")]]
      [:p
        [:button {:type "submit"} "Submit"]]]
    `[:ul ,@(for [m :in messages]
              [:li (format "%s: %s" (m :name) (m :message))])]])

(defn index-html [messages params errors]
  (let [body (content messages params errors)]
    (html
      (doctype :html5)
      head
      body)))
